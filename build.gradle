buildscript {
    repositories {
        gradlePluginPortal()
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

allprojects { proj ->
    apply plugin: "maven-publish"
    apply plugin: "signing"

    group = 'com.swingnosefrog.shoehorn'
    version = '0.2.0'

    repositories {
        mavenCentral()
    }

    // Default POM attributes
    publishing {
        repositories {
            maven {
                url = uri(rootProject.file(".m2-temp/"))
                name = "TempLocal"
            }
        }
        publications {
            maven(MavenPublication) {
                pom {
                    name = "${group}:${proj.getName()}"
                    description = "The ${proj.getName()} subproject of shoehorn"
                    url = "https://github.com/chrislo27/shoehorn"
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'swingnosefrog'
                            name = 'Chris'
                            email = 'chrislo27@users.noreply.github.com'
                            organization = "GitHub"
                            organizationUrl = "https://github.com/chrislo27"
                        }
                    }
                    scm {
                        connection = 'scm:git:git://github.com/chrislo27/shoehorn.git'
                        developerConnection = 'scm:git:ssh://github.com:chrislo27/shoehorn.git'
                        url = 'https://github.com/chrislo27/shoehorn'
                    }
                }
            }
        }
    }
    
    signing {
        useGpgCmd()
        sign publishing.publications.maven
    }

    if (System.getenv("JITPACK") == "true") {
        // Jitpack generates the wrong Gradle .module data and causes missing native dependencies (classifiers).
        // https://github.com/jitpack/jitpack.io/issues/4476
        tasks.withType(GenerateModuleMetadata).configureEach {
            enabled = false
        }
    }
}

subprojects {
    apply plugin: "java-library"
    apply plugin: "kotlin"
    
    java {
        withJavadocJar()
        withSourcesJar()
    }
}


//#region Root project only
apply plugin: "java-platform"

dependencies {
    constraints {
        api project(":core")
        api project(":di")
        api project(":json")
        api project(":webserver")
        api project(":database")
        api project(":database:sqlite")
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId = 'shoehorn'

            from components.javaPlatform
            
            pom {
                description = "Shoehorn: A utility project to collate webserver microservice dependencies and utilities"
            }
        }
    }
}

//#endregion
